apply plugin: 'maven-publish'
apply plugin: 'signing'

//TODO: check if sources are being included
java {
    withSourcesJar()
}

group = maven_group
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId maven_group
                artifactId archives_base_name
                version PUBLISH_VERSION

                artifact(remapJar) {
                    builtBy remapJar
                }
                artifact(kotlinSourcesJar) {
                    classifier = "sources"
                    builtBy remapSourcesJar
                }

                // Mostly self-explanatory metadata
                pom {
                    name = archives_base_name
                    description = 'Fabric mod that adds NBT and PacketByteBuf to kotlinx.serialization'
                    url = github_url
                    licenses {
                        license {
                            name = license
                        }
                    }
                    developers {
                        developer {
                            id = "fudge"
                            name = "natan"
                            email = "natandestroyer100@gmail.com"
                        }
                    }

                    scm {
                        url = github_url
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}